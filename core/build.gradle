apply plugin: "application"

applicationName = "kerala-server"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.5"
    }
}

dependencies {
    compile project(":shared")
    compile project(":proto")

    compile group: "commons-cli", name: "commons-cli", version: "1.4"
    compile "log4j:log4j:1.2.17"

    testCompile "org.hamcrest:hamcrest-junit:2.+"
    testCompile group: "com.nhaarman.mockitokotlin2", name: "mockito-kotlin", version: "2.2.0"
}

sourceSets {
    main {
        java {
            srcDirs "core/build/generated/source/proto/main/grpc"
            srcDirs "core/build/generated/source/proto/main/java"
        }
    }
}

mainClassName = "org.kerala.core.BootKt"

task copyLicense {
    outputs.file new File("$buildDir/LICENSE")
    doLast {
        copy {
            from "LICENSE"
            into "$buildDir"
        }
    }
}

applicationDistribution.from(copyLicense) {
    into ""
}

applicationDistribution.into("bin") {
    fileMode = 0755
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
